// C:\Users\user\Desktop\Apa pendrive\DataFlex\Projects\CarDealerShip\AppSrc\CarSelect.wo
// Car
//

Use cWebView.pkg
Use cWebList.pkg
Use cWebMenuGroup.pkg
Use cWebMenuItem.pkg
Use cWebColumnButton.pkg
Use cWebColumn.pkg
Use CarZoom.wo

Use cFuelDataDictionary.dd
Use cGearboxDataDictionary.dd
Use cCarsDataDictionary.dd
Use cDepartmentDataDictionary.dd
Use cWebAppUserDataDictionary.dd
Use cWebAppSessionDD.dd
Use cWebForm.pkg

Object oCarSelect is a cWebView
    Set psCaption to "Car"
    Set peWebViewStyle to wvsDrilldown
    Set peViewType to vtSelect
    Set piColumnCount to 12
    Set pbShowCaption to False
    Set piMaxWidth to 1024

    Object oDepartment_DD is a cDepartmentDataDictionary
    End_Object

    Object oWebAppUser_DD is a cWebAppUserDataDictionary
        Set DDO_Server to oDepartment_DD
    End_Object

    Object oWebAppSession_DD is a cWebAppSessionDD
        Set DDO_Server to oWebAppUser_DD
    End_Object

    Object oFuel_DD is a cFuelDataDictionary
    End_Object 

    Object oGearbox_DD is a cGearboxDataDictionary
    End_Object 

    Object oCars_DD is a cCarsDataDictionary
        Set DDO_Server To oFuel_DD
        Set DDO_Server to oGearbox_DD
        
        // EN
        // Limit the rows to the search value
        // GE
        // Zeilen auf den Suchwert beschränken
        // HU
        // Korlátozza a sorokat a keresési értékre
        Procedure OnConstrain
            String sValue sFilter
            
            // EN
            // The value from whatever was entered. It comes from oWebform1
            // GE
            // Der Wert aus dem, was eingegeben wurde. Es kommt von oWebform1
            // HU
            // Az érték bármiből, amit beírtak. Az oWebform1-ből származik
            WebGet psValue of oWebForm1 to sValue
            Move (Trim(sValue)) to sValue
            If (sValue > "") Begin                
                Move "[CarId] LIKE '%@@%' or [Model] LIKE '%@@%' or [Color] LIKE '%@@%' or [NumberOfSeats] LIKE '%@@%'" to sFilter
                
                // EN
                // Replace all with the value entered by the user
                // GE
                // Alle durch den vom Benutzer eingegebenen Wert ersetzen
                // HU
                // Cserélje le az összeset a felhasználó által beírt értékre
                Move (Replace("@@", sFilter, sValue)) to sFilter
                Move (Replace("@@", sFilter, sValue)) to sFilter
                Move (Replace("@@", sFilter, sValue)) to sFilter
                Move (Replace("@@", sFilter, sValue)) to sFilter
                                                
                // EN
                // In this case, we want to use the PS SQL filter property of the data dictionary to filter automatically
                // It only works within a table
                // GE
                // In diesem Fall möchten wir die PS-SQL-Filtereigenschaft des Datenwörterbuchs zum automatischen Filtern verwenden
                // Es funktioniert nur innerhalb einer Tabelle
                // HU
                // Ebben az esetben az adatszótár PS SQL filter tulajdonságát szeretnénk használni, hogy automatikus szűrjünk
                // Csak táblázaton belül működik
                Set psSQLFilter to sFilter 
                
                // EN
                // We need to configure the data dictionary to use SQL filters
                // GE
                // Wir müssen das Datenwörterbuch für die Verwendung von SQL-Filtern konfigurieren
                // HU
                // Tályékoztatnunk kell az adatszótárt, hogy használjon SQL szűrőket
                Set pbUseDDSQLFilters to True
            End
            Else Begin
                Set pbUseDDSQLFilters to False
            End
        End_Procedure
    End_Object 

    // EN
    // Main_DD startet die Ansicht, führt alle Einschränkungen aus,
    // stellt sicher, dass Sie auf die Daten in den Ansichten zugreifen können
    // GE
    // Main_DD startet die Ansicht, führt alle Einschränkungen aus,
    // stellt sicher, dass Sie auf die Daten in den Ansichten zugreifen können
    // HU
    // a Main_DD indítja el a nézetet, megtesz minden korlátozást,
    // gondoskodik arról, hogy hozzáférhessen az adatokhoz a nézetekben
    Set Main_DD to oCars_DD
    
    // EN
    // If we change something and want to save it, the Server takes care of it
    // GE
    // Wenn wir etwas ändern und es speichern wollen, kümmert sich der Server darum
    // HU
    // Ha meváltoztatunk valamit és el szeretnénk menteni, arról a Server gondoskodik
    Set Server  To oCars_DD

    Object oWebForm1 is a cWebForm
        Set piColumnSpan to 0
        Set psLabel to "Search"
        Set peLabelPosition to lpTop
        Set pbPromptButton to True
        Procedure OnPrompt
            // EN
            // When clicked, we rebuild it according to the search conditions
            // so we send the constraints to the data dictionary
            // GE
            // Wenn darauf geklickt wird, erstellen wir es entsprechend den Suchbedingungen neu
            // Also senden wir die Einschränkungen an das Datenwörterbuch
            // HU
            // Kattintásnál újraépítjük a keresési feltéleknek megfelelően
            // ezért elküldjük az adatszótárnak a megszorításokat
            Send Rebuild_Constraints of oCars_DD
            // EN
            // The list must be linked again. We find the first record and then rebuild it.
            // This should be implemented in the data dictionary
            // GE
            // Die Liste muss erneut verknüpft werden. Wir finden den ersten Datensatz und erstellen ihn dann neu.
            // Dies sollte im Datenwörterbuch implementiert werden
            // HU
            // A listát újra össze kell kapcsolni. Megkeressük az első rekordot, majd újraépítjük.
            // Ezt az adatszótárban kell megvalósítani
            Send Find of oCars_DD FIRST_RECORD 1            
        End_Procedure
    End_Object


    Object oList is a cWebList
        Set piColumnSpan to 0
        Set psCSSClass to "MobileList"
        Set pbServerOnRowClick to True
        Set pbFillHeight to True
        Set pbShowHeader to True // Set pbShowHeader to False
        Set piSortColumn to 0

        Object oCarsCarId is a cWebColumn
            
            // EN
            // Entry_Item can also be replaced with a Procedure
            // GE
            // Entry_Item kann auch durch eine Prozedur ersetzt werden
            // HU
            // egy Procedure-rel is helyettesíthető az Entry_Item 
            Entry_Item Cars.CarId
            Set psCaption to "CarId"
            Set piWidth to 100
        End_Object 

        Object oCarsModel is a cWebColumn
            Entry_Item Cars.Model
            Set psCaption to "Model"
            Set piWidth to 200
        End_Object 

        Object oCarsColor is a cWebColumn
            Entry_Item Cars.Color
            Set psCaption to "Color"
            Set piWidth to 200
        End_Object 

        Object oFuelFuelName is a cWebColumn
            Entry_Item Fuel.FuelName
            Set psCaption to "Fuel"
            Set piWidth to 100
        End_Object 

        Object oGearboxGearboxName is a cWebColumn
            Entry_Item Gearbox.GearboxName
            Set psCaption to "Gearbox"
            Set piWidth to 130
        End_Object 

        Object oCarsNumberOfSeats is a cWebColumn
            Entry_Item Cars.NumberOfSeats
            Set psCaption to "NumberOfSeats"
            Set piWidth to 200
        End_Object 

        Object oDetailButton is a cWebColumnButton
            Set psCaption to "btn"
            Set piWidth to 45
            Set pbFixedWidth to True
            Set pbResizable to False
            Set piListRowSpan to 1
            Set psBtnCssClass to "WebButtonIcon WebIcon_Info"
            Set peAlign to alignRight

            WebRegisterPath ntNavigateForward oCarZoom

            Procedure OnClick
                Send NavigatePath
            End_Procedure

            Procedure OnGetNavigateForwardData tWebNavigateData ByRef NavigateData Handle hoToView
                Move True to NavigateData.bReadOnly
            End_Procedure

        End_Object 

        WebRegisterPath ntNavigateForward oCarZoom

        Procedure OnRowClick String sRowId
            tWebNavigateData NavigateData
            Get GetNavigateData to NavigateData

            Case Begin
                Case (NavigateData.eNavigateType = nfFromParent)
                    Send NavigateForward of oCarZoom Self
                    Case Break

                Case (NavigateData.eNavigateType = nfFromChild)
                    Send NavigateClose Self
                    Case Break

                Case (NavigateData.eNavigateType = nfFromMain)
                    Send NavigateClose Self
                    Case Break

                Case Else // must be nfUndefined
                    Send NavigateForward of oCarZoom Self

            Case End
        End_Procedure

        Procedure OnGetNavigateForwardData tWebNavigateData ByRef NavigateData Handle hoToView
            Move True to NavigateData.bReadOnly
        End_Procedure

    End_Object 

    Object oActionGroup is a cWebMenuGroup

        Object oSearch is a cWebMenuItem
            Set psCSSClass to "WebPromptMenuItem"
            Set psCaption to "Search"

            Procedure OnClick
                Send Search of oList
            End_Procedure

        End_Object 

        Object oNewButton is a cWebMenuItem
            Set psCSSClass to "WebClearMenuItem"
            Set psCaption to "New"

            WebRegisterPath ntNavigateForward oCarZoom

            Procedure OnClick
                Send NavigatePath
            End_Procedure

            Procedure OnGetNavigateForwardData tWebNavigateData ByRef NavigateData Handle hoToView
                Move True to NavigateData.bNewRecord
            End_Procedure

        End_Object 
    End_Object 

    Procedure OnNavigateForward tWebNavigateData NavigateData Integer hoInvokingView Integer hoInvokingObject
        WebSet pbRender of oNewButton to True
        WebSet pbRender of oDetailButton to True

        Case Begin
            Case (NavigateData.eNavigateType = nfFromParent)
                // If from parent, this is a constrained drill down
                Case Break

            Case (NavigateData.eNavigateType = nfFromChild)
                // If from child, this is a probably a parent lookup from a Zoom
                Case Break

            Case (NavigateData.eNavigateType = nfFromMain)
                // If from main, this is a probably a main-file lookup from a Zoom
                WebSet pbRender of oDetailButton to False
                Case Break

            Case Else // must be nfUndefined
                // This may be the start of a drilldown query or this may be used for some kind of custom query.

        Case End

    End_Procedure


End_Object 
